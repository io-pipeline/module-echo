import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.plugins.signing.Sign

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.21.0'
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.2.1'
}

scmVersion {
    tag {
        prefix = 'v'
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

def pipelineBomVersion = findProperty('pipelineBomVersion') ?: '0.2.2'

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    // Import pipeline BOM to align all managed versions
    implementation platform("ai.pipestream:pipeline-bom:${pipelineBomVersion}")

    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.quarkus:quarkus-arc'

    // Core Quarkus dependencies
    implementation("io.quarkus:quarkus-rest")
    implementation("io.quarkus:quarkus-rest-jackson")
    implementation("io.quarkus:quarkus-smallrye-health")
    implementation("io.quarkus:quarkus-smallrye-openapi")
    
    // Service Discovery dependencies for Consul
    implementation("io.quarkus:quarkus-smallrye-stork")
    implementation("io.smallrye.stork:stork-service-discovery-consul")
    implementation("io.smallrye.reactive:smallrye-mutiny-vertx-consul-client")

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation("io.rest-assured:rest-assured")
    testImplementation libs.hamcrest
    testImplementation libs.hamcrest.library
    
    // Mock services for testing
//    testImplementation project(':grpc:grpc-wiremock')
    testImplementation libs.pipestream.grpc.wiremock
    // Pipestream libraries - versions managed entirely by the BOM/catalog
    implementation libs.pipestream.compose.devservices
    implementation libs.pipestream.grpc.stubs
    implementation libs.pipestream.commons
    implementation libs.pipestream.data.util
    implementation libs.pipestream.api
    implementation libs.pipestream.dynamic.grpc
    implementation libs.pipestream.dynamic.grpc.registration.clients
}

group = "ai.pipestream.module"
version = "${scmVersion.version}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    // Increase memory for large message testing
    jvmArgs '-Xmx4g', '-Xms1g'
}

// Integration test configuration for large messages
tasks.withType(Test) {
    if (name.contains('IntTest')) {
        jvmArgs '-Xmx6g', '-XX:MaxDirectMemorySize=2g'
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Configure Javadoc task
tasks.withType(Javadoc).configureEach { task ->
    def opts = task.options
    opts.encoding = 'UTF-8'
    opts.charSet = 'UTF-8'
    opts.locale = 'en'
    // Suppress missing Javadoc warnings for now
    opts.addStringOption('Xdoclint:-missing', '-quiet')
    // Exclude internal packages
    task.exclude('**/internal/**', '**/impl/**', '**/test/**')
}

// Enable resource filtering to replace @version@ in application.properties
processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}

processTestResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}

// Publishing configuration
publishing {
    publications {
        // Use a named publication to match signing configuration below
        create('maven', MavenPublication) {
            from components.java
            pom {
                name.set('Module Echo')
                description.set('Echo module for Pipestream')
                url.set('https://github.com/ai-pipestream/module-echo')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/license/mit')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Engine Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/module-echo.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/module-echo.git')
                    url.set('https://github.com/ai-pipestream/module-echo')
                }

                // Ensure BOM/platform dependency is included in dependencyManagement
                withXml {
                    def projectNode = asNode()
                    def dependencyManagementNodes = projectNode.get('dependencyManagement')
                    def dmNode
                    def depsNode

                    if (dependencyManagementNodes.isEmpty()) {
                        dmNode = projectNode.appendNode('dependencyManagement')
                        depsNode = dmNode.appendNode('dependencies')
                    } else {
                        dmNode = dependencyManagementNodes[0]
                        def dependenciesNodes = dmNode.get('dependencies')
                        if (dependenciesNodes.isEmpty()) {
                            depsNode = dmNode.appendNode('dependencies')
                        } else {
                            depsNode = dependenciesNodes[0]
                        }
                    }

                    def existingBom = depsNode.children().find { dep ->
                        dep.groupId?.text() == 'ai.pipestream' &&
                        dep.artifactId?.text() == 'pipeline-bom'
                    }

                    if (!existingBom) {
                        def bomDependency = depsNode.appendNode('dependency')
                        bomDependency.appendNode('groupId', 'ai.pipestream')
                        bomDependency.appendNode('artifactId', 'pipeline-bom')
                        bomDependency.appendNode('version', pipelineBomVersion)
                        bomDependency.appendNode('type', 'pom')
                        bomDependency.appendNode('scope', 'import')
                    }
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Conditionally add GitHub Packages repository when credentials are available
        def ghRepo = System.getenv('GITHUB_REPOSITORY') ?: 'ai-pipestream/module-echo'
        def ghActor = System.getenv('GITHUB_ACTOR') ?: (project.findProperty('gpr.user') ?: System.getenv('GPR_USER'))
        def ghToken = System.getenv('GITHUB_TOKEN') ?: (project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN'))
        if (ghActor && ghToken) {
            maven {
                name = 'GitHubPackages'
                url = uri("https://maven.pkg.github.com/${ghRepo}")
                credentials {
                    username = ghActor
                    password = ghToken
                }
            }
        }
    }
}

signing {
    // Read org-level GPG secrets directly
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Ensure all signing tasks run before nmcp bundles and publishes artifacts
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}